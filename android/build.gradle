// Build instructions for Android library.

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.2.3'
        classpath 'com.jakewharton.sdkmanager:gradle-plugin:0.12.0'
        classpath 'org.robolectric:robolectric-gradle-plugin:1.1.0'
    }
}

apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.library'
apply plugin: 'org.robolectric'

android {
    compileSdkVersion 19
    buildToolsVersion "19.1"

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 19
        versionCode = VERSION_CODE
        versionName = VERSION_NAME
    }

    lintOptions {
        abortOnError false
    }
}

// Set the source and target to Java 6.
tasks.withType(JavaCompile) {
    options.compilerArgs << "-source" << "1.6" << "-target" << "1.6"
}

android.libraryVariants.all { variant ->
    def name = variant.buildType.name

    // For each variant add a task to copy the core classes so they are included in the AAR.
    def copyTask = project.tasks.create(name: "copyCoreToAndroid${name.capitalize()}", type: Copy) {
        from(project(':core').sourceSets.main.output)
        into(variant.javaCompile.destinationDir)
    }
    copyTask.dependsOn variant.javaCompile
    variant.outputs.each { output ->
        output.packageLibrary.dependsOn copyTask
    }

    // Also define a task to create a raw JAR to support non-AAR users.
    def rawJarTask = project.tasks.create(name: "rawAndroidJar${name.capitalize()}", type: Jar) {
        archiveName="keen-client-api-android-${project.version}.jar"
        from(variant.javaCompile.destinationDir)
    }
    rawJarTask.dependsOn copyTask
}

configurations {
    // Exclude modules that are provided by Android to avoid compilation warning.
    all*.exclude module: 'commons-logging'
    all*.exclude module: 'httpclient'
}

dependencies {
    compile project(':core')
    androidTestCompile('org.robolectric:robolectric:2.3') {
        exclude module: "support-v4"
    }
    androidTestCompile 'org.hamcrest:hamcrest-library:1.3'
    androidTestCompile 'junit:junit:4.11'
    androidTestCompile 'org.mockito:mockito-core:1.9.5'
}

archivesBaseName = "keen-client-api-android"

apply from: file("${parent.rootDir}/gradle/gradle-mvn-publish-android.gradle")
