// Build instructions for Android library.

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.0'
    }
}

apply plugin: 'com.android.library'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.0"

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 25
        versionCode = VERSION_CODE.toInteger()
        versionName = VERSION_NAME
    }

    lintOptions {
        abortOnError false
    }
}

// Set the source and target to Java 6.
tasks.withType(JavaCompile) {
    options.compilerArgs << "-source" << "1.6" << "-target" << "1.6"
}

android.libraryVariants.all { variant ->
    def name = variant.buildType.name

    // Also define a task to create a raw JAR to support non-AAR users.
    def rawJarTask = project.tasks.create(name: "rawAndroidJar${name.capitalize()}", type: Jar) {
        archiveName="keen-client-api-android-${project.version}.jar"
        from(variant.javaCompile.destinationDir)
    }
    rawJarTask.dependsOn variant.javaCompile
}

configurations {
    // Exclude modules that are provided by Android to avoid compilation warning.
    all*.exclude module: 'commons-logging'
    all*.exclude module: 'httpclient'
}

dependencies {
    compile project(':core')
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    testCompile 'junit:junit:4.11'
    testCompile 'org.mockito:mockito-core:1.9.5'
}

archivesBaseName = "keen-client-api-android"

apply from: file("${parent.rootDir}/gradle/gradle-mvn-publish-android.gradle")
