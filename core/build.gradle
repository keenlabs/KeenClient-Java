// Build instructions for core library.

apply plugin: 'java-library'

compileJava {
    sourceCompatibility = '1.7'
    targetCompatibility = '1.7'
}

task verifyVersionFile {
    def versionFile = file("$projectDir/src/main/java/io/keen/client/java/KeenVersion.java")

    doFirst {
        if (!versionFile.text.contains("SDK_VERSION = \"$project.version\"")) {
            throw new IllegalStateException("Project version set in KeenVersion.java does not match version set in gradle.properties")
        }
    }
}

compileJava.dependsOn verifyVersionFile

dependencies {
    testImplementation 'com.fasterxml.jackson.core:jackson-databind:2.15.2'
    testImplementation 'commons-io:commons-io:2.13.0'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.hamcrest:hamcrest:2.2'
    testImplementation 'org.mockito:mockito-core:5.5.0'
}

jar {
    manifest {
        attributes("Implementation-Vendor": "Keen.io", "Implementation-Version": version)
    }
}

test {
    include '**/*Test.class'
}

archivesBaseName = "keen-client-java-core"

apply from: file("${parent.rootDir}/gradle/gradle-mvn-publish-java.gradle")
