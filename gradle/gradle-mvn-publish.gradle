apply plugin: 'maven-publish'
apply plugin: 'signing'

def isReleaseBuild() {
    return !version.endsWith("SNAPSHOT")
}

def sonatypeRepositoryUrl
if (isReleaseBuild()) {
    sonatypeRepositoryUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
} else {
    sonatypeRepositoryUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
}
publishing {
    publications {
        maven(MavenPublication) {
            groupId = GROUP
            artifactId = POM_ARTIFACT_ID
            version = VERSION_NAME

            afterEvaluate {
                from components.java
            }

            pom {
                name = POM_NAME
                packaging = POM_PACKAGING
                description = POM_DESCRIPTION

                if (project.toString().contains('android')) {
                    packaging = 'aar'
                }

                url = POM_URL

                organization {
                    name = POM_ORG_NAME
                    url = POM_ORG_URL
                }

                scm {
                    url = POM_SCM_URL
                    connection = POM_SCM_CONNECTION
                    developerConnection = POM_SCM_DEV_CONNECTION
                }

                licenses {
                    license {
                        name = POM_LICENSE_NAME
                        url = POM_LICENSE_URL
                        distribution = POM_LICENSE_DIST
                    }
                }

                developers {
                    developer {
                        id = POM_DEVELOPER_ID
                        name = POM_DEVELOPER_NAME
                        email = POM_DEVELOPER_EMAIL
                    }
                }
            }
        }
    }
    repositories {
        mavenLocal()
        maven {
            url = sonatypeRepositoryUrl

            credentials {
                username = project.sonatypeUsername
                password = project.sonatypePassword
            }
        }
    }
}

signing {
    required {
        isReleaseBuild() && gradle.taskGraph.hasTask("publish")
    }
    sign publishing.publications.maven
}
